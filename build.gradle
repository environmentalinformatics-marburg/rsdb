// gradle version 9.1.0

plugins {
  //id 'org.kordamp.gradle.markdown' version '2.2.0' // https://github.com/aalmiray/markdown-gradle-plugin //Workaround disable markdownToHtml, reason: "Could not find remark-1.1.0.jar (com.overzealous:remark:1.1.0)"
  id "com.github.ben-manes.versions" version '0.53.0' // update check task: dependencyUpdates  https://github.com/ben-manes/gradle-versions-plugin
}

// check for updates:
//gradle dependencyUpdates

// list (transitive) dependencies
//gradle dependencies

// upgrade gradle wrapper
//./gradlew wrapper --gradle-version=9.2.0

def isNonStable = { String version ->
  return version.contains('alpha') || version.contains('beta') 
}

dependencyUpdates {
  rejectVersionIf {  // reject all non stable versions
    isNonStable(it.candidate.version)
  }
}

/*markdownToHtml { //Workaround disable markdownToHtml, reason: "Could not find remark-1.1.0.jar (com.overzealous:remark:1.1.0)"
	sourceDir file('webfiles')
	outputDir file('package/webfiles')
}*/

apply plugin: 'java'
apply plugin: 'eclipse'
compileJava.options.encoding = 'UTF-8'

//remove gdal dependency if loaded in eclipse for manual gdal library specification
eclipse {
  classpath {
    file {
      whenMerged { classpath ->
        classpath.entries.removeIf { it instanceof org.gradle.plugins.ide.eclipse.model.Library && it.moduleVersion.toString() == 'org.gdal:gdal:3.4.0' }
      }
    }
  }
}

repositories {
    mavenCentral()
    
    /*maven { // needed for compile group: 'com.github.aelstad', name: 'keccakj', version: '1.1.0', server not always reachable
      url 'https://repository.mulesoft.org/nexus/content/repositories/public'
    }*/
    
    flatDir { // local jar files, keccakj
       dirs 'lib'
   	}
}

dependencies {
	implementation 'org.tinylog:tinylog-impl:2.8.0-M1'
	runtimeOnly 'org.tinylog:slf4j-tinylog:2.8.0-M1'
  	
  	implementation 'org.mapdb:mapdb:2.0-beta13' // warning: newer versions v3.x do break storage layout
  	
  	implementation 'org.json:json:20250517'
  	implementation 'org.yaml:snakeyaml:2.5'
  	implementation 'com.opencsv:opencsv:5.12.0'  	
  	implementation 'com.github.mreutegg:laszip4j:0.21'
  	implementation 'ar.com.hjg:pngj:2.1.0'
  	implementation 'com.samskivert:jmustache:1.16'	
  	
  	implementation 'org.apache.commons:commons-math3:3.6.1'
  	implementation 'com.mkobos:pca_transform:1.0.2'
  	
  	implementation 'com.googlecode.javaewah:JavaEWAH:1.2.3'  	
  	implementation 'org.xerial.snappy:snappy-java:1.1.10.8' // caution: newer versions may break storage layout (last tested: 1.1.7.7)
    implementation 'me.lemire.integercompression:JavaFastPFOR:0.1.12' // version 0.2.1 compiled to new class file version not compatible with java 11; caution: newer versions may break storage layout  // data format changed from 0.1.11 to 0.1.12 
  	implementation 'com.github.luben:zstd-jni:1.5.7-6' // caution: newer versions may break storage layout (last tested: 1.4.5-6)
  	
  	implementation 'org.antlr:antlr4-runtime:4.9.3' // caution: newer versions may break with generated DSL Java source files
  	
	implementation 'org.eclipse.jetty:jetty-server:11.0.26' // keep v11.x, newer Java version is needed for v12.x
	implementation 'org.eclipse.jetty:jetty-security:11.0.26' // keep v11.x, newer Java version is needed for v12.x
	implementation 'org.eclipse.jetty.http2:http2-server:11.0.26' // keep v11.x, newer Java version is needed for v12.x
	implementation 'org.eclipse.jetty:jetty-alpn-java-server:11.0.26' // keep v11.x, newer Java version is needed for v12.x
	implementation 'org.eclipse.jetty:jetty-client:11.0.26' // keep v11.x, newer Java version is needed for v12.x
	
	implementation 'io.jsonwebtoken:jjwt-api:0.11.5' // TODO: check for breaking changes in v0.12.x
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5' // TODO: check for breaking changes in v0.12.x
    runtimeOnly 'io.jsonwebtoken:jjwt-gson:0.11.5' // TODO: check for breaking changes in v0.12.x	
	
	compileOnly 'org.gdal:gdal:3.4.0' // compileOnly because gdal library from executing system is needed; caution: newer versions may break compatibility
	implementation 'org.locationtech.jts:jts-core:1.20.0'
	implementation 'org.locationtech.proj4j:proj4j:1.4.1'
	implementation 'org.locationtech.proj4j:proj4j-epsg:1.4.1'	
	
	//implementation 'org.imintel:mbtiles4j:1.0.6' // mbtiles currently not used
	
	//implementation 'com.github.aelstad:keccakj:1.1.0' // server not always reachable
	
	implementation 'net.postgis:postgis-jdbc:2025.1.1'		
}

sourceSets.main.java.srcDir 'src'
sourceSets.main.java.srcDir 'dsl/generated-sources'
sourceSets.main.java.srcDir 'src_testing'

jar {
	from file('src/tinylog.properties')
	destinationDirectory = project.layout.projectDirectory.dir('package')
	
	manifest {
		String classPathFiles = "";
		for(java.io.File file : files(configurations.runtimeClasspath)) {
			classPathFiles += "lib/"+file.getName()+" "; 
		}
		//println("the class path: "+classPathFiles);	
	
        attributes 	'Built-Date': new Date(),
					'Built-JDK': System.getProperty('java.version'),
					'Main-Class': 'run.Terminal',
					'Class-Path': classPathFiles
    }
}

java {
  sourceCompatibility = '21' // build compatiblity with java 11 and newer
  targetCompatibility = '21' // run compatiblity with java 11 and newer
}

tasks.withType(JavaCompile){
    //options.deprecation = true // generate warnings for depricated
	//options.listFiles = true
	//options.verbose = true
	options.compilerArgs << '-Xlint:unchecked' // generate warnings for unchecked
}

task clean_package(type: Delete) {
  delete 'package'
}

task copy_lib(type: Copy) {
	from files(configurations.runtimeClasspath)
    into 'package/lib'
}

task copy_add(type: Copy) {
	from fileTree('add')
	into 'package'
}

task copy_webcontent(type: Copy) {
	from fileTree('webcontent')
	into 'package/webcontent'
}

task copy_webfiles(type: Copy) {
	from fileTree('webfiles')
	into 'package/webfiles'
}

task copy_mustache(type: Copy) {
	from fileTree('mustache')
	into 'package/mustache'
}

/*task copy_webfiles_with_markdown(dependsOn: markdownToHtml) { //convert markdown to html and copy all other files //Workaround disable markdownToHtml, reason: "Could not find remark-1.1.0.jar (com.overzealous:remark:1.1.0)"
}*/

task copy_build(type: Copy, dependsOn: [jar]) {
	from fileTree('build/libs')
	into 'package'
}

//task _package(dependsOn:[ clean_package, copy_build, copy_lib, copy_webcontent, copy_webfiles_with_markdown, copy_mustache, copy_add ]) { //Workaround disable markdownToHtml, reason: "Could not find remark-1.1.0.jar (com.overzealous:remark:1.1.0)"
task _package(dependsOn:[ clean_package, copy_build, copy_lib, copy_webcontent, copy_webfiles, copy_mustache, copy_add ]) { //Workaround disable markdownToHtml, reason: "Could not find remark-1.1.0.jar (com.overzealous:remark:1.1.0)"
	group = 'project' 
}